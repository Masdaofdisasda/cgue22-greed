#version 430 core

in vec3 fColor;
in vec3 fNormal;
in vec3 fPosition;

// light sources
// use vec4 instead vec3 for GLSL std140 data layout standard
struct DirectionalLight
{
	vec4 direction;

	vec4 ambient;
    vec4 diffuse;
    vec4 specular;
};

struct PositionalLight
{
	vec4 position;
	vec4 attenuation;

	vec4 ambient;
    vec4 diffuse;
    vec4 specular;
};

struct SpotLight
{
	vec4 position;
	vec4 direction;
	vec4 Angles;
	vec4 attenuation;

	vec4 ambient;
    vec4 diffuse;
    vec4 specular;
};

struct Material
{
	vec4 ambient;
	vec4 diffuse;
	vec4 specular;
	vec4 shininess;
};

// light source data in uniform blocks
// MAXLIGHTS gets replaced at runtime
layout (std140, binding = 0) uniform dLightUBlock {
 DirectionalLight dLights [ dMAXLIGHTS ];
};
uniform uint dLightCount ;

layout (std140, binding = 1) uniform pLightUBlock {
 PositionalLight pLights [ pMAXLIGHTS ];
};
uniform uint pLightCount ;

layout (std140, binding = 2) uniform sLightUBlock {
 SpotLight sLights [ sMAXLIGHTS ];
};

uniform uint sLightCount ;

uniform Material material;

uniform vec3 viewPos;

out vec4 FragColor;

// material parameters
vec3 albedo = fColor;
float metallic = material.ambient.x;
float roughness = material.ambient.y;
float ao = material.ambient.z;

const float PI = 3.14159265359;


float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness*roughness;
    float a2 = a*a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}

vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

void main()
{		
    vec3 N = normalize(fNormal);
    vec3 V = normalize(viewPos - fPosition);

    // calculate reflectance at normal incidence; if dia-electric (like plastic) use F0 
    // of 0.04 and if it's a metal, use the albedo color as F0 (metallic workflow)    
    vec3 F0 = vec3(0.04); 
    F0 = mix(F0, albedo, metallic);

    // reflectance equation
    vec3 Lo = vec3(0.0);
    for(int i = 0; i < pLightCount; ++i) 
    {
        // calculate per-light radiance
        vec3 L = normalize(vec3(pLights[i].position) - fPosition);
        vec3 H = normalize(V + L);
        float distance = length(vec3(pLights[i].position) - fPosition);
        float attenuation = 1.0 / (distance * distance);
        vec3 radiance = vec3(3000.0 * pLights[i].ambient) * attenuation;

        // Cook-Torrance BRDF
        float NDF = DistributionGGX(N, H, roughness);   
        float G = GeometrySmith(N, V, L, roughness);    
        vec3 F = fresnelSchlick(clamp(dot(H, V), 0.0, 1.0), F0);        
        
        vec3 numerator    = NDF * G * F;
        float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.0001; // + 0.0001 to prevent divide by zero
        vec3 specular = numerator / denominator;
        
         // kS is equal to Fresnel
        vec3 kS = F;
        // for energy conservation, the diffuse and specular light can't
        // be above 1.0 (unless the surface emits light); to preserve this
        // relationship the diffuse component (kD) should equal 1.0 - kS.
        vec3 kD = vec3(1.0) - kS;
        // multiply kD by the inverse metalness such that only non-metals 
        // have diffuse lighting, or a linear blend if partly metal (pure metals
        // have no diffuse light).
        kD *= 1.0 - metallic;	                
            
        // scale light by NdotL
        float NdotL = max(dot(N, L), 0.0);        

        // add to outgoing radiance Lo
        Lo += (kD * albedo / PI + specular) * radiance * NdotL; // note that we already multiplied the BRDF by the Fresnel (kS) so we won't multiply by kS again
    }   
    
    vec3 ambient = vec3(0.03) * albedo * ao;

    vec3 color = ambient * Lo;

    // HDR tonemapping
    color = color / (color + vec3(1.0));
    // gamma correct
    color = pow(color, vec3(1.0/2.2)); 
    
    FragColor = vec4(color, 1.0);
}